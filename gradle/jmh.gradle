//
// Configure JMH benchmarks
//

sourceSets {
    jmh {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

dependencies {
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.26'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.26'

    jmhImplementation 'net.openhft:affinity:3.20.0'
}

jmh {
    jmhVersion = '1.26'
    jvmArgsPrepend =[ '-Xms256m', '-Xmx256m', '-XX:MaxDirectMemorySize=1g' ]
    resultsFile = project.file("${project.buildDir}/reports/jmh/result.json")
    profilers = [ 'pauses' ]
    resultFormat = 'JSON'
    failOnError = true
    verbosity = 'NORMAL'
    includeTests = true
}

task jmhProfilers(type: JavaExec, description:'Lists the available profilers for the jmh task', group: 'jmh') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'
    args '-lprof'
}
